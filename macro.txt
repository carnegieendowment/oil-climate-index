' Module level variables (for scoping purposes)
Private Oils As Integer
Private NonSpecialOils As Integer
Private OCICS As Workbook
Private OCICSMain As Worksheet
Private StartCell As Range
Private OilNames As Range

Sub Data_Processing()
'
' Data_Processing Macro
' Uses this workbook in conjunction with the OPGEE and PRELIM models to generate data for the OCI website
' This assumes that the workbook starts with the default OPGEE and PRELIM runs in place
' The macro DOES NOT save this file after running (so it will stay in the original state)
'
' Created by Development Seed
'
  
  Application.ScreenUpdating = False
  Application.Calculation = xlCalculationManual
  
  ' This Workbook is the OCICS
  Set OCICS = ThisWorkbook
  Set OCICSMain = OCICS.Sheets("OCI")
  
  ' Store our starting cell
  Set StartCell = OCICSMain.Range("I5")
  
  ' Return the number of columns until our first empty cell, going right
  Oils = Range(StartCell, StartCell.End(xlToRight)).Columns.Count
  
  ' Unique Oil Names to be used throughout
  Set OilNames = Range(StartCell.Offset(1, 0), StartCell.Offset(1, Oils - 1))
  
  ''' INFO
  Call Info
  
  ''' OPGEE
  Call OPGEE
  
  ''' PRELIM
  Call PRELIM
   
  Application.ScreenUpdating = True
  
'
End Sub

Sub Info()

  ''' OIL INFO
  Dim InfoBook As Workbook
  
  ' Create a new workbook (to be titled info.csv)
  Set InfoBook = Workbooks.Add
      
  ' Copy rows 5-27 for all oils, paste transposed into info.csv
  Range(StartCell, StartCell.Offset(22, Oils - 1)).Copy
  InfoBook.Sheets("Sheet1").Range("A2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
     SkipBlanks:=False, Transpose:=True
  
  ' Grab the variable names from column F
  Range(StartCell.Offset(0, -3), StartCell.Offset(22, -3)).Copy
  InfoBook.Sheets("Sheet1").Range("A1").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
     SkipBlanks:=False, Transpose:=True
  
  ' Copy row 273-275 (Field Age, Field Depth, Oil Production Volume) for all oils, paste transposed into info.csv
  Range(StartCell.Offset(268, 0), StartCell.Offset(270, Oils - 1)).Copy
  InfoBook.Sheets("Sheet1").Range("X2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
     SkipBlanks:=False, Transpose:=True

  InfoBook.Sheets("Sheet1").Range("X1").Value = "Field Age"
  InfoBook.Sheets("Sheet1").Range("Y1").Value = "Field Depth"
  InfoBook.Sheets("Sheet1").Range("Z1").Value = "Oil Production Volume"
  
  ' Copy row 453 (default refinery) for all oils, paste transposed into info.csv
  Range(StartCell.Offset(448, 0), StartCell.Offset(448, Oils - 1)).Copy
  InfoBook.Sheets("Sheet1").Range("AA2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
     SkipBlanks:=False, Transpose:=True
  InfoBook.Sheets("Sheet1").Range("AA1").Value = "Default Refinery"
  
  ' Copy row 1065 (transport emissions) for all oils, paste transposed into info.csv
  Range(StartCell.Offset(1060, 0), StartCell.Offset(1060, Oils - 1)).Copy
  InfoBook.Sheets("Sheet1").Range("AB2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
     SkipBlanks:=False, Transpose:=True
  InfoBook.Sheets("Sheet1").Range("AB1").Value = "Transport Emissions"
  
  ' Copy row 1091 (total emissions) for all oils, paste transposed into info.csv
  Range(StartCell.Offset(1086, 0), StartCell.Offset(1086, Oils - 1)).Copy
  InfoBook.Sheets("Sheet1").Range("AC2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
     SkipBlanks:=False, Transpose:=True
  InfoBook.Sheets("Sheet1").Range("AC1").Value = "Total Emissions"
  
  ' Save and close info.csv
  Application.DisplayAlerts = False
  InfoBook.SaveAs Filename:=OCICS.Path & "\info.csv", FileFormat:=xlCSV
  InfoBook.Close
  Application.DisplayAlerts = True

End Sub

Sub OPGEE()

  Dim OPGEE As Workbook
  Dim OPGEEBook As Workbook
  Dim OPGEEModel As Workbook
  Dim OPGEEInputs As Worksheet
  Dim OPGEEOutputs As Worksheet
  Dim MetadataBook As Workbook
  Dim MetadataSheet As Worksheet
  Dim OPGEEModelPath As String
  Dim WaterArray() As String
  Dim SteamArray() As String
  Dim FlareArray() As String
  
  ' Open the OPGEE Model (let the user choose it)
  OPGEEModelPath = Application.GetOpenFilename(Title:="Select OPGEE Model")
  Set OPGEEModel = Workbooks.Open(OPGEEModelPath)
  Set OPGEEInputs = OPGEEModel.Sheets("Bulk Assessment - Inputs")
  Set OPGEEOutputs = OPGEEModel.Sheets("Bulk Assessment - Results")
  
  ' Go grab our model parameters from Column A (Row 69,73,85) of OCICS
  ' They are stored in a string array so we'll use CDbl(Array(index)) when accessing
  WaterArray() = Split(OCICSMain.Range("A69").Value, ",")
  SteamArray() = Split(OCICSMain.Range("A73").Value, ",")
  FlareArray() = Split(OCICSMain.Range("A85").Value, ",")
    
  ' Loop over our OPGEE Parameters

  ' For OPGEE we won’t run the Special Oils so leave them out (requires a “tag” in row 4)

  NonSpecialOils = Range(StartCell.Offset(-1, 0), StartCell.Offset(-1, 0).End(xlToRight)).Columns.Count - 1
  
  For w = LBound(WaterArray) To UBound(WaterArray)
      For s = LBound(SteamArray) To UBound(SteamArray)
          For f = LBound(FlareArray) To UBound(FlareArray)
              ' Create a new workbook (to be titled opgee_run[xxx].csv)
              Set OPGEEBook = Workbooks.Add
              
              ' Copy inputs from OCICS to OPGEE
              Range(StartCell.Offset(30, 0), StartCell.Offset(107, NonSpecialOils - 1)).Copy
              OPGEEInputs.Range("H9").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=False
              
              ''' Run Model Once to get Imputed Values
              ' It needs automatic calculations on
              ' It requires that book to be activated or it's macro references fail
              ' We need to specify how many oils we are running it on
              ' We need to disable calculation on OCICS or the calculations will lock up the system
              
               Application.Calculation = xlCalculationAutomatic
               For Each sh In OCICS.Worksheets
                 sh.EnableCalculation = False
               Next
               OPGEEInputs.Range("C2").Value = Oils
               OPGEEModel.Activate
               Application.Run ("" & OPGEEModel.Name & "!UltraBulk_assessment")
               
               ' Copy imputed water, steam, and flaring values on to Inputs tab
               Range(OPGEEOutputs.Range("H43"), OPGEEOutputs.Range("H44").Offset(0, NonSpecialOils - 1)).Copy
               OPGEEInputs.Range("H43").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=False
                  
               Range(OPGEEOutputs.Range("H47"), OPGEEOutputs.Range("H47").Offset(0, NonSpecialOils - 1)).Copy
               OPGEEInputs.Range("H47").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=False
                  
               Range(OPGEEOutputs.Range("H59"), OPGEEOutputs.Range("H59").Offset(0, NonSpecialOils - 1)).Copy
               OPGEEInputs.Range("H59").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=False
               
               ' Scale according to our run
               For i = 0 To (NonSpecialOils - 1)
                   OPGEEInputs.Range("H43").Offset(0, i).Value = OPGEEInputs.Range("H43").Offset(0, i).Value * CDbl(WaterArray(w))
                   OPGEEInputs.Range("H44").Offset(0, i).Value = OPGEEInputs.Range("H44").Offset(0, i).Value * CDbl(WaterArray(w))
                   OPGEEInputs.Range("H47").Offset(0, i).Value = OPGEEInputs.Range("H47").Offset(0, i).Value * CDbl(SteamArray(s))
                   OPGEEInputs.Range("H59").Offset(0, i).Value = OPGEEInputs.Range("H59").Offset(0, i).Value * CDbl(FlareArray(f))
               Next
               
               ' Run Again
               Application.Run ("" & OPGEEModel.Name & "!UltraBulk_assessment")
               Application.Calculation = xlCalculationManual
               Application.ScreenUpdating = False
              
              ' (4) Copy & Paste (Outputs and Output-Inputs)
              
              Range(OPGEEModel.Sheets("Bulk Assessment - Results").Range("H9"), OPGEEModel.Sheets("Bulk Assessment - Results").Range("H88").Offset(0, NonSpecialOils - 1)).Copy
              StartCell.Offset(257, 0).PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=False
              
              Range(OPGEEModel.Sheets("Bulk Assessment - Results").Range("H103"), OPGEEModel.Sheets("Bulk Assessment - Results").Range("H157").Offset(0, NonSpecialOils - 1)).Copy
              StartCell.Offset(351, 0).PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=False
              
              ' Calculate
              For Each sh In OCICS.Worksheets
                 sh.EnableCalculation = True
               Next
              Application.Calculate
              
              ' (5) Write to OPGEEBook and save
              
              ' Run Name
              OPGEEBook.Sheets("Sheet1").Range("A1").Value = "Run"
              For i = 0 To (Oils - 1)
                OPGEEBook.Sheets("Sheet1").Range("A2").Offset(i, 0).Value = "run" & w & s & f
              Next i
              
              ' Unique Oil Names
              OilNames.Copy
              OPGEEBook.Sheets("Sheet1").Range("B1").Value = "Unique"
              OPGEEBook.Sheets("Sheet1").Range("B2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=True
              
              ' Values
              Range(StartCell.Offset(417, 0), StartCell.Offset(429, Oils - 1)).Copy
              OPGEEBook.Sheets("Sheet1").Range("C2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=True
              
              ' Variable Names (column F)
              Range(StartCell.Offset(417, -3), StartCell.Offset(429, -3)).Copy
              OPGEEBook.Sheets("Sheet1").Range("C1").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=True
                  
              ' Output-Inputs
              ' 283 (API Gravity) 295 (GOR) 296 (WOR)
              OPGEEBook.Sheets("Sheet1").Range("P1").Value = "API Gravity"
              Range(StartCell.Offset(278, 0), StartCell.Offset(278, Oils - 1)).Copy
              OPGEEBook.Sheets("Sheet1").Range("P2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=True
              OPGEEBook.Sheets("Sheet1").Range("Q1").Value = "Gas-to-Oil-Ratio"
              OPGEEBook.Sheets("Sheet1").Range("R1").Value = "Water-to-Oil-Ratio"
              Range(StartCell.Offset(290, 0), StartCell.Offset(291, Oils - 1)).Copy
              OPGEEBook.Sheets("Sheet1").Range("Q2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=True
                  
              ' Save and close .csv
              Application.DisplayAlerts = False
              OPGEEBook.SaveAs Filename:=OCICS.Path & "\opgee_run" & w & s & f & ".csv", FileFormat:=xlCSV
              OPGEEBook.Close
              Application.DisplayAlerts = True
          Next f
      Next s
  Next w
  
  ' Close model, force no-save
  OPGEEModel.Close SaveChanges:=False
  
  ' Write out metadata
  Set MetadataBook = Workbooks.Add
  Set MetadataSheet = MetadataBook.Sheets("Sheet1")
  MetadataSheet.Range("A1").Value = "water"
  MetadataSheet.Range("A2").Value = OCICSMain.Range("A69").Value
  MetadataSheet.Range("B1").Value = "steam"
  MetadataSheet.Range("B2").Value = OCICSMain.Range("A73").Value
  MetadataSheet.Range("C1").Value = "flare"
  MetadataSheet.Range("C2").Value = OCICSMain.Range("A85").Value
  MetadataSheet.Range("D1").Value = "refinery"
  MetadataSheet.Range("D2").Value = StartCell.Offset(965, -2).Value
  
  Application.DisplayAlerts = False
  MetadataBook.SaveAs Filename:="metadata.csv", FileFormat:=xlCSV
  MetadataBook.Close
  Application.DisplayAlerts = True
  
End Sub

Sub PRELIM()

  Dim PRELIM As Workbook
  Dim PRELIMSheet As Worksheet
  Dim RefineryRange As Range
  
  Set RefineryRange = Range(StartCell.Offset(965, 0), StartCell.Offset(965, Oils - 1))
  
  ' Combustion Variables - Format
  For j = 0 To 6
    StartCell.Offset(970 + j, -3).Value = "Portion " + StartCell.Offset(970 + j, -3).Value
  Next j
  
  ' 5 loops (refinery types)
  For i = 0 To 4
      ' New PRELIM Output Sheet
      Set PRELIM = Workbooks.Add
      Set PRELIMSheet = PRELIM.Sheets("Sheet1")
      
      ' Set Refinery
      For Each cell In RefineryRange.Cells
          cell.Value = i
      Next
      
      ' Calculate
      Application.Calculate
      
      ' Copy and Paste
      
      ' Run Name
      PRELIMSheet.Range("A1").Value = "Run"
      For j = 0 To (Oils - 1)
        PRELIMSheet.Range("A2").Offset(j, 0).Value = "run" & i
      Next j
      
      ' Unique Oil Names
      OilNames.Copy
      PRELIMSheet.Range("B1").Value = "Unique"
      PRELIMSheet.Range("B2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
                  SkipBlanks:=False, Transpose:=True
      
      ' PRELIM Values
      Range(StartCell.Offset(1007, 0), StartCell.Offset(1028, Oils - 1)).Copy
      PRELIMSheet.Range("C2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
          SkipBlanks:=False, Transpose:=True
          
      ' PRELIM Variable Names (column F)
      Range(StartCell.Offset(1007, -3), StartCell.Offset(1028, -3)).Copy
      PRELIMSheet.Range("C1").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
          SkipBlanks:=False, Transpose:=True
          
      ' Combustion Values
      Range(StartCell.Offset(1066, 0), StartCell.Offset(1073, Oils - 1)).Copy
      PRELIMSheet.Range("Y2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
          SkipBlanks:=False, Transpose:=True
      
      ' Combustion Variables
      Range(StartCell.Offset(1066, -3), StartCell.Offset(1073, -3)).Copy
      PRELIMSheet.Range("Y1").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
          SkipBlanks:=False, Transpose:=True
          
      ' Combustion Values - Pricing
      Range(StartCell.Offset(970, 0), StartCell.Offset(976, Oils - 1)).Copy
      PRELIMSheet.Range("AG2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
          SkipBlanks:=False, Transpose:=True
      
      ' Combustion Variables - Pricing
      Range(StartCell.Offset(970, -3), StartCell.Offset(976, -3)).Copy
      PRELIMSheet.Range("AG1").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
          SkipBlanks:=False, Transpose:=True
         
      ' MJ Conversion
      PRELIMSheet.Range("AN1").Value = "MJperbbl"
      Range(StartCell.Offset(1103, 0), StartCell.Offset(1103, Oils - 1)).Copy
      PRELIMSheet.Range("AN2").PasteSpecial Paste:=xlPasteValues, Operation:=xlNone, _
          SkipBlanks:=False, Transpose:=True
         
      ' Clean Sheet
      PRELIMSheet.Columns(13).EntireColumn.Delete
      PRELIMSheet.Columns(9).EntireColumn.Delete
      PRELIMSheet.Columns(6).EntireColumn.Delete
      PRELIMSheet.Columns(4).EntireColumn.Delete
      
      ' Save Sheet
      
      Application.DisplayAlerts = False
      PRELIM.SaveAs Filename:=OCICS.Path & "\prelim_run" & i & ".csv", FileFormat:=xlCSV
      PRELIM.Close
      Application.DisplayAlerts = True
  
  Next i

End Sub

